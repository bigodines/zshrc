function prompt_caius_precmd {
  local exitstatus=$?
  setopt localoptions noxtrace noksharrays
  psvar=(SIG)
  [[ $exitstatus -gt 128 ]] && psvar[1]=SIG$signals[$exitstatus-127]
  [[ $psvar[1] = SIG ]] && psvar[1]=$exitstatus
  jobs % >/dev/null 2>&1 && psvar[2]=
}

function prompt_caius_battery {
  if [[ ! -f /usr/bin/pmset ]]; then
    return ""
  fi

  if [[ "$(pmset -g | awk '/Active Profiles/, /Currently in use/ { if (!/Active Profiles/ && !/Currently in use/) print }' | wc -l)" -eq 2 ]]; then
    pmset -g batt | tail -1 | awk '{
      # TODO: handle AC Power not charging;
      if ($3 == "charging;") {
        icon="%1{⚡️%}"
      } else if ($3 == "charged;") {
        icon="🌟"
      } else {
        icon="🔋"
      }
      sub(";", "", $2)
      sub("%", "%%", $2)
      output = sprintf("%s %s ", $2, icon)
      if ($4 != "0:00") { output = sprintf("%s (%s)", output, $4) }
      print output
    }'
  fi
}

function prompt_caius_datetime {
  date "+%Y-%m-%d %H:%M:%S"
}

function prompt_caius_ruby {
  type chruby > /dev/null && (chruby | awk '/\*/ { sub("ruby-", "", $2); print $2 }')
}

function prompt_caius_setup {
#  PS1='[%(2L.%L/.)'$ZSH_VERSION']%(?..%B{%v}%b)%n%(2v.%B@%b.@)%m:%B%~%b%(!.#.>) '
  [[ ! -z "$SSH_CONNECTION" ]] && sshflag="☎︎ "
  PS1='$sshflag%{$fg_bold[green]%}%m:%{$fg_bold[blue]%}%1~$(git_prompt_info) %{$reset_color%}%n%(!.#.$) '
  PS2='%(4_:... :)%3_> '

  [[ "Darwin" == $(uname) ]] && RPROMPT="\$(prompt_caius_ruby) | \$(prompt_caius_battery) | \$(prompt_caius_datetime)"
  [[ -z $RPROMPT ]] && RPROMPT="\$(prompt_caius_datetime)"

  prompt_opts=( cr subst percent )

  add-zsh-hook precmd prompt_caius_precmd
}

prompt_caius_setup "$@"
